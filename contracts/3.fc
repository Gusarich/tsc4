{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

int ubitsize(int) asm "UBITSIZE";
int test_builder(builder, int) asm "BCHKBITSQ";
int remaining_bits(builder) asm "BREMBITS";

int pfx(int x, int l, int n) inline asm """
    // x l n
    SUB // x l-n
    RSHIFT // x>>(l-n)
""";

int sfx(int x, int n) inline asm """
    // x n
    DUP // x n n
    IF:<{
        DEC // x n-1
        POW2 // x 1<<(n-1)
        DUP // x 1<<(n-1) 1<<(n-1)
        DEC // x 1<<(n-1) 1<<(n-1)-1
        ADD // x (1<<n)-1
        AND // x&((1<<n)-1)
    }>ELSE<{
        DROP2
        ZERO
    }>
""";

int substr(int x, int l, int a, int b) inline {
    return (x & (((1 << (b - a)) - 1) << (l - b))) >> (l - b);
}

(tuple, builder) write(tuple res, builder cur, int val, int szv) inline {
    if (cur.test_builder(szv)) {
        return (res, cur.store_uint(val, szv));
    }
    int x = cur.remaining_bits();
    int y = szv - x;
    return (cons(cur.store_uint(val >> y, x), res), begin_cell().store_uint(sfx(val, y), y));
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
    slice s = linked_list.begin_parse();

    int sz = ubitsize(flag);
    int szv = ubitsize(value);

    tuple res = null();
    builder cur = begin_cell();

    int w = min(s.slice_bits(), 256);
    int x = s~load_uint(w);

    while (w >= sz) {
        int i = 0;
        int last = 0;
        int e = min(128, w - sz);
        while (i <= e) {
            int y = substr(x, w, i, i + sz);
            if (y == flag) {
                (res, cur) = write(res, cur, (substr(x, w, last, i) << szv) + value, (i - last) + szv);
                i += sz;
                last = i;
            } else {
                i += 1;
            }
        }

        if (last < i) {
            (res, cur) = write(res, cur, substr(x, w, last, i), i - last);
        }

        int q = s.slice_bits();
        int x' = 0;
        int w' = 0;
        if (q >= i) {
            x' = s~load_uint(i);
            w' = i;
        } else {
            x' = s~load_uint(q);
            w' = q;
            if (~ s.slice_refs_empty?()) {
                s = s~load_ref().begin_parse();
                q = min(i - w', s.slice_bits());
                x' = (x' << q) + s~load_uint(q);
                w' += q;
            }
        }
        x = (sfx(x, w - i) << w') + x';
        w = (w - i) + w';
    }

    (res, cur) = write(res, cur, x, w);

    while (~ null?(res)) {
        cur = (res~list_next()).store_ref(cur.end_cell());
    }

    return cur.end_cell();
}

() recv_internal() {
}