{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

int ubitsize(int) asm "UBITSIZE";
forall X -> X tpop(tuple) asm "TPOP";
forall X -> tuple unsafe_tuple(X) asm "NOP";
int tlen(tuple) asm "TLEN";
forall X -> X last(tuple) asm "LAST";
forall X -> int is_null(X) asm "ISNULL";

(slice, slice, int) load_tail(slice a, slice b, int len) {
    int x = a.slice_bits();
    if (x >= len) {
        int r = a~load_uint(len);
        return (a, b, r);
    } else {
        int y = b.slice_bits();
        if (x + y >= len) {
            int r = (a~load_uint(x) << (len - x)) + b~load_uint(len - x);
            if (b.slice_refs_empty?()) {
                return (b, "", r);
            }
            slice c = b~load_ref().begin_parse();
            return (b, c, r);
        } else {
            return ("", "", 0);
        }
    }
}

int preload_tail(slice a, slice b, int len) {
    int x = a.slice_bits();
    if (x >= len) {
        return a.preload_uint(len);
    } else {
        int y = b.slice_bits();
        if (x + y >= len) {
            return (a.preload_uint(x) << (len - x)) + b.preload_uint(len - x);
        } else {
            return 0;
        }
    }
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
    slice s = linked_list.begin_parse();
    slice next = s.slice_refs_empty?() ? "" : s~load_ref().begin_parse();

    int sz = ubitsize(flag);
    int szv = ubitsize(value);

    tuple result = null();
    builder cur = begin_cell();

    do {
        ~dump([s, next]);
        int x = preload_tail(s, next, sz);
        if (x == flag) {
            if (cur.builder_bits() + szv > 1023) {
                result = cons(cur, result);
                cur = begin_cell();
            }
            (s, next, _) = load_tail(s, next, sz);
            cur~store_uint(value, szv);
        } else {
            if (cur.builder_bits() == 1023) {
                result = cons(cur, result);
                cur = begin_cell();
            }
            (s, next, int y) = load_tail(s, next, 1);
            cur~store_uint(y, 1);
        }
    } until (s.slice_empty?());
    
    while (~ result.is_null()) {
        cur = (result~list_next()).store_ref(cur.end_cell());
    }

    return cur.end_cell();
}

() recv_internal() {
}