{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

int ubitsize(int) asm "UBITSIZE";
int test_builder(builder, int) asm "BCHKBITSQ";
slice substr(slice, int, int) asm "SDSUBSTR";

;; (tuple, builder) write(tuple res, builder cur, builder val) inline {
;;     if (cur.test_builder(val.builder_bits())) {
;;         return (res, cur.store_builder(val));
;;     }
;;     return (cons(cur, res), begin_cell().store_builder(val));
;; }

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
    slice s = linked_list.begin_parse();

    int sz = ubitsize(flag);
    int szv = ubitsize(value);
    slice flag_s = begin_cell().store_uint(flag, sz).end_cell().begin_parse();

    tuple res = null();
    builder cur = begin_cell();

    int w = min(s.slice_bits(), 256);
    builder x = begin_cell().store_uint(s~load_uint(w), w);

    while (w >= sz) {
        slice xs = x.end_cell().begin_parse();
        int i = 0;
        int last = 0;
        int e = w - sz;
        while (i <= e) {
            slice y = xs.substr(i, sz);
            if (y.equal_slice_bits(flag_s)) {
                int d = i - last;
                ifnot (cur.test_builder(d + szv)) {
                    (res, cur) = (cons(cur, res), begin_cell());
                }
                cur = cur.store_slice(xs.substr(last, d)).store_uint(value, szv);
                i += sz;
                last = i;
            } else {
                i += 1;
            }
        }

        if (last < i) {
            ifnot (cur.test_builder(i - last)) {
                (res, cur) = (cons(cur, res), begin_cell());
            }
            cur = cur.store_slice(xs.skip_bits(last).preload_bits(i - last));
        }
        
        x = begin_cell().store_slice(xs.skip_bits(i));

        int q = s.slice_bits();
        if (q >= i) {
            x~store_uint(s~load_uint(i), i);
        } else {
            x~store_uint(s~load_uint(q), q);
            ifnot (s.slice_refs_empty?()) {
                s = s~load_ref().begin_parse();
                x = x.store_slice(s~load_bits(min(i - q, s.slice_bits())));
            }
        }
        w = x.builder_bits();
    }

    (res, cur) = (cons(cur, res), x);

    while (~ null?(res)) {
        cur = (res~list_next()).store_ref(cur.end_cell());
    }

    return cur.end_cell();
}

() recv_internal() {
}