{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}

() recv_internal() {
}

forall X -> int is_null(X) asm "ISNULL";

;; testable
(cell) find_branch_by_hash(int hash, cell tree) method_id {
    tuple q = null();
    q = cons(tree, q);
    while (~ is_null(q)) {
        (cell c, q) = uncons(q);
        if (c.cell_hash() == hash) {
            return c;
        }
        slice s = c.begin_parse();
        repeat (s.slice_refs()) {
            q = cons(s~load_ref(), q);
        }
    }
    return begin_cell().end_cell();
}
